_[
    -- 設定 --
    S: 設定値スケール（100ならば、1の時0.01を表す）
    W: 幅（回）
    H: 高さ（回）
    X: C_0_re
    Y: C_0_im
    D: C_i+1_re - C_i_re
    E: C_i+1_im - C_i_im
    N: 発散判定の試行回数
    L: 行のセパレータ文字
    C: 発散しなかった箇所の文字
    B: 出力する文字のASCIIコードの最低値

    -- 内部変数 --
    I: 発散判定の閾値（4 * S^2）
    T: Sの100倍

    a-d: zの計算用変数
    s: 符号を保存する一次変数
    t-u: 一時変数
    w: 残り列数
    x: C_i_re(x1E-2)
    h: 残り行数
    y: C_i_im(x1E-2)
]

1000S=
200W=
80H=
0X= 2 000-
0Y= 1 200-
15D=
30E=
40N=
10L=
32C=
65B=

100S?T=
4S??I=

Y?y=
H?h=
[
    X?x=
    W?w=
    [
        0a=
        0b=
        N?i=[
            
            S?x?c= a??c+ b??c-
            S?y?d= 2a?b?d+

            0s=
            0t= c?t- [2t?- s= _] 2t?- t?c=
            0a= c[100a+ T?c-] 0t= c?t- [T?c+ 100a-_]
            c[a+ S?c-] 0t= c?t- [a-_]
            s[2a?-_]

            0s=
            0t= d?t- [2t?- s= _] 2t?- t?d=
            0b= d[100b+ T?d-] 0t= d?t- [T?d+ 100b-_]
            d[b+ S?d-] 0t= d?t- [b-_]
            s[2b?-_]

            a??r= b??r+
            I?t=r?t-2[_2]

            i-
        ]

        r=i[0r= B?i+ . _]r[C. _]

        D?x+ w-
    ]

    L.
    0w= X?x=
    E?y+ h-
]